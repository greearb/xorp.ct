# Copyright (c) 2009 XORP, Inc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, Version 2, June
# 1991 as published by the Free Software Foundation. Redistribution
# and/or modification of this program under the terms of any other
# version of the GNU General Public License is not permitted.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. For more details,
# see the GNU General Public License, Version 2, a copy of which can be
# found in the XORP LICENSE.gpl file.
#
# XORP Inc, 2953 Bunker Hill Lane, Suite 204, Santa Clara, CA 95054, USA;
# http://xorp.net

# $ID$

import os
Import("env")

subdirs = [ 'tests' ]

SConscript(dirs = subdirs, exports='env')

env = env.Clone()
is_shared = 'SHAREDLIBS' in env

env.AppendUnique(CPPPATH = [
    '#',
    '$BUILDDIR',
    ])

libxorp_fea_client_srcs = [
	# C++ files
	'ifmgr_atoms.cc',
	'ifmgr_cmds.cc',
	'ifmgr_cmd_queue.cc',
	'ifmgr_xrl_replicator.cc',
	'ifmgr_xrl_mirror.cc',
	]

if is_shared:
    libxorp_fea_client = env.SharedLibrary(target = 'libxorp_fea_client',
					   source = libxorp_fea_client_srcs)
    if env['rtld_origin']:
        for obj in libxorp_fea_client:
            env.AddPostAction(libxorp_fea_client,
                env.Symlink(obj.abspath,
                            os.path.join(env['xorp_alias_libdir'], str(obj))))
else:
    libxorp_fea_client = env.StaticLibrary(target = 'libxorp_fea_client',
					   source = libxorp_fea_client_srcs)

if is_shared:
    env.Alias('install',
        env.InstallLibrary(env['xorp_libdir'], libxorp_fea_client))

Default(libxorp_fea_client)
