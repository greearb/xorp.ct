# Copyright (c) 2009-2011 XORP, Inc and Others
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, Version 2, June
# 1991 as published by the Free Software Foundation. Redistribution
# and/or modification of this program under the terms of any other
# version of the GNU General Public License is not permitted.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. For more details,
# see the GNU General Public License, Version 2, a copy of which can be
# found in the XORP LICENSE.gpl file.
#
# XORP Inc, 2953 Bunker Hill Lane, Suite 204, Santa Clara, CA 95054, USA;
# http://xorp.net

# $ID$

import os
Import("env")

env = env.Clone()

env.AppendUnique(CPPPATH = [
    '#',
    '$BUILDDIR',
    ])

is_shared = 'SHAREDLIBS' in env

sources = [
	# C++ files
	'netlink_socket.cc',
	'netlink_socket_utilities.cc',
	'routing_socket.cc',
	'routing_socket_utilities.cc',
	'windows_rras_support.cc',
	'windows_rtm_pipe.cc'
	]

if env['enable_click']:
    sources.append('click_socket.cc')

if is_shared:
    libxcs = env.SharedLibrary(target = 'libxorp_fea_control_socket',
			       source = sources)
    if env['rtld_origin']:
        for obj in libxcs:
            env.AddPostAction(libxcs,
                env.Symlink(obj.abspath,
                            os.path.join(env['xorp_alias_libdir'], str(obj))))
    env.Alias('install', env.InstallLibrary(env['xorp_libdir'], libxcs))
else:
    libxcs = env.StaticLibrary(target = 'libxorp_fea_control_socket',
			       source = sources)

Default(libxcs)
