dnl -*- sh -*-
dnl $XORP: xorp/mibs/configure.in,v 1.1 2004/11/19 05:19:09 bms Exp $
dnl

dnl ---------------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script.
dnl ---------------------------------------------------------------------------

dnl ---------------------------------------------------------------------------
dnl Boilerplate
dnl ---------------------------------------------------------------------------

AC_PREREQ(2.13)

AC_INIT
AC_CONFIG_AUX_DIR(../config)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(mibs, 1.0)
AM_MAINTAINER_MODE

dnl ----------------------------
dnl libtool options
dnl ----------------------------
AC_DISABLE_STATIC
AC_DISABLE_FAST_INSTALL
AC_PROG_LIBTOOL

AC_VALIDATE_CACHED_SYSTEM_TUPLE(
	echo "Removing stale config.cache and restarting configure"
	rm -f config.cache
	exec ./configure
)

dnl ---------------------------------------------------------------------------
dnl Command line options
dnl ---------------------------------------------------------------------------

dnl ----------------------------
dnl --disable-debug
dnl ----------------------------
AC_ARG_ENABLE(debug,
	[  --disable-debug                  disable debug messages ],
	[],
	[enable_debug=yes;]			dnl XXX: enable by default
)

dnl ----------------------------
dnl --enable-debug-msgs
dnl ----------------------------
AC_ARG_ENABLE(debug-msgs,
	[  --enable-debug-msgs              enable debug messages ],
)

dnl ----------------------------
dnl --enable-debug-fnames (GCC)
dnl ----------------------------
AC_ARG_ENABLE(debug-fnames,
	[  --enable-debug-fnames            enable function names in debug messages
                                   (requires GCC)],
)

dnl ----------------------------
dnl --enable-optimize
dnl ----------------------------
AC_ARG_ENABLE(optimize,
	[  --enable-optimize                enable compiler optimizations],
)

dnl ----------------------------
dnl --enable-profile
dnl ----------------------------
AC_ARG_ENABLE(profile,
	[  --enable-profile                 turn on profiling],
)

dnl ----------------------------
dnl --enable-callback-debug
dnl ----------------------------
AC_ARG_ENABLE(callback-debug,
	[  --enable-callback-debug          enable callback debugging ],
	AC_DEFINE(DEBUG_CALLBACKS, 1,
	          [Define to enable callback debugging])
	,
)

dnl ----------------------------
dnl --disable-ipv6
dnl ----------------------------
AC_ARG_ENABLE(ipv6,
	[  --disable-ipv6                   disable IPv6 support],
	[],
	[enable_ipv6=yes;]			dnl XXX: enable by default
)


dnl ----------------------------
dnl --with-openssl
dnl ----------------------------
AC_ARG_WITH(openssl,
	[  --with-openssl=DIR               specify root of OpenSSL installation ],
	[ xr_openssl_prefix="$withval" ]
)

dnl ----------------------------
dnl --with-path-to-snmpd
dnl ----------------------------
AC_ARG_WITH(path-to-snmpd,
	[  --with-path-to-snmpd=DIR         specify path to the snmpd executable],
	[snmpd=${withval}/snmpd;], [snmpd="not_found";]
)

dnl ----------------------------
dnl --with-path-to-net-snmp-src
dnl ----------------------------
AC_ARG_WITH(path-to-net-snmp-src,
	[  --with-path-to-net-snmpd-src=DIR specify path to the net-snmpd source tree],
	[path_to_net_snmp_src=${withval};], [path_to_net_snmp_src="no";]
)

dnl ----------------------------
dnl --without-snmp
dnl ----------------------------
AC_ARG_WITH(snmp,
	[  --without-snmp                   do not build XORP MIBs for Net-SNMP],
	[snmp="no";], [snmp="yes";]
)


dnl ---------------------------------------------------------------------------
dnl Compiler probing
dnl ---------------------------------------------------------------------------

AC_PROG_CC

AC_PROG_CXX
AC_PROG_CXXCPP

AC_LANG_CPLUSPLUS

dnl
dnl Include the macros for testing compiler-supported command-line options
dnl
builtin(include, ../config/compiler_flags.m4)


dnl ----------------------------
dnl  Get project-specific macros
dnl ----------------------------

builtin(include, ../config/acxorp.m4)

dnl ---------------------------------------------------------------------------
dnl Additional tool probes
dnl ---------------------------------------------------------------------------

dnl ----------------------------
dnl CPP GNU variable arguments
dnl ----------------------------

AC_CACHE_CHECK(
	[whether preprocessor supports GNU style variable argument macros],
	xorp_cv_cpp_gnu_va_args,
	AC_TRY_CPP(
		[
			#define SOME_PRINTF(args...) printf(args)
			SOME_PRINTF("hello %s %d", "world", '\n');
		],
		xorp_cv_cpp_gnu_va_args=yes,
		xorp_cv_cpp_gnu_va_args=no
	)
)
if test "${xorp_cv_cpp_gnu_va_args}" = "yes" ; then
	AC_DEFINE(CPP_SUPPORTS_GNU_VA_ARGS, 1,
 [Define to 1 if the preprocessor supports GNU style variable argument macros])
fi

AC_PROG_INSTALL
AC_PROG_LN_S

AC_CACHE_CHECK([if GNU make is installed], xorp_cv_gmake_name,
                for g in ${GMAKE} make gmake gnumake; do
		  if ($g -version) < /dev/null > /dev/null 2>&1 ; then
		    GMAKE_NAME="$g"
		    break
		  fi
		done
		xorp_cv_gmake_name=${GMAKE_NAME})

dnl ----------------------------
dnl OpenSSL (Strictly OpenSSL MD5 implementation)
dnl ----------------------------

fail_openssl()
{
	echo "Could not find part of OpenSSL or one it's components in $1"
	echo "Use --with-openssl=DIR to specify OpenSSL directory"
	exit 1
}

if test "${xr_openssl_prefix}" = "" ; then
# User has not specified an OpenSSL prefix, may be in cache, otherwise take
# a guess
    AC_CACHE_CHECK(
	[OpenSSL installation prefix],
	xr_cv_openssl_prefix,
	[
	    # Prefer base install over local install, assume base if not found
	    for xr_cv_openssl_prefix in /usr /usr/local /usr ; do
		if test -d ${xr_cv_openssl_prefix}/include/openssl; then
		    break
		fi
	    done
	])
elif test "${xr_openssl_prefix}" != "${xr_cv_openssl_prefix}"; then

    # User has specified a new OpenSSL prefix, flush cache state of
    # header files and libraries that we care about to force a
    # re-check.
    unset ac_cv_header_openssl_md5_h
    unset ac_cv_lib_crypto_MD5_Init
    unset xr_cv_openssl_prefix

    # This cache check is doomed to fail because we unset variable,
    # but use it for consistent looking output.
    AC_CACHE_CHECK([OpenSSL installation prefix],
		   xr_cv_openssl_prefix,
		   xr_cv_openssl_prefix="${xr_openssl_prefix}")
fi

if test -d ${xr_cv_openssl_prefix}/include/openssl; then
    if test "${xr_cv_openssl_prefix}" != "/usr"; then
	CPPFLAGS="${CPPFLAGS} -I${xr_cv_openssl_prefix}/include"
	LIBS="${LIBS} -L${xr_cv_openssl_prefix}"
    fi
    AC_HEADER_CHECK(openssl/md5.h, , fail_openssl "${xr_cv_openssl_prefix}")
    AC_CHECK_LIB(crypto, MD5_Init, , fail_openssl "${xr_cv_openssl_prefix}")
else
    fail_openssl "${xr_cv_openssl_prefix}"
fi

dnl ---------------------------------------------------------------------------
dnl Check for programs
dnl ---------------------------------------------------------------------------

dnl ---------------------------------------------------------------------------
dnl SNMP
dnl ---------------------------------------------------------------------------

if test "${snmp}" = "yes"; then
    AC_CHECK_PROG(net_snmp_found, net-snmp-config, "true", "false")
else
    net_snmp_found="skip_check"
    snmpd="skip_check"
fi

if test "${net_snmp_found}" = "true";  then
    net_snmp_v=`net-snmp-config --version`
    net_snmp_maj=`echo $net_snmp_v | cut -f 1 -d.`
    net_snmp_min=`echo $net_snmp_v | cut -f 2 -d.`
    net_snmp_rev=`echo $net_snmp_v | cut -f 3 -d.`
    dnl
    dnl XXX: if the revision number is missing, then give it value of 0
    dnl
    if test "${net_snmp_rev}" = ""; then
	net_snmp_rev=0
    fi
    AC_CACHE_CHECK([if net-snmp version equal or newer than 5.0.6],
    ac_cv_net_snmp_usable,
	net_snmp_too_old=true
	while test "${net_snmp_too_old}" = "true"; do
	    if test "${net_snmp_maj}" -lt "5" ; then
		break;
	    fi
	    if test "${net_snmp_maj}" -gt "5" ; then
		net_snmp_too_old=false
		break;
	    fi
	    if test "${net_snmp_min}" -lt "0" ; then
		break;
	    fi
	    if test "${net_snmp_min}" -gt "0" ; then
		net_snmp_too_old=false
		break;
	    fi
	    if test "${net_snmp_rev}" -lt "6" ; then
		break;
	    fi
	    if test "${net_snmp_rev}" -gt "6" ; then
		net_snmp_too_old=false
		break;
	    fi
	    net_snmp_too_old=false
	    break;
	done
	if test "${net_snmp_too_old}" != "true"; then
	    ac_cv_net_snmp_usable=yes
	else
	    ac_cv_net_snmp_usable=no
	    snmpd="skip_check"
	fi
    )
    if test "${ac_cv_net_snmp_usable}" != "yes" ; then
	AC_MSG_WARN(net-snmp version too old)
    fi
elif test "${net_snmp_found}" = "false"; then
    AC_MSG_WARN(Net-SNMP not found! MIB modules will not be built)
    snmpd="skip_check"
fi


if test "${snmpd}" = "not_found"; then
    AC_PATH_PROG(snmpd, snmpd, "not_found", /usr/local/sbin)
fi

if test "${snmpd}" = "not_found";  then
    echo "checking again for snmpd in another directory"
    AC_PATH_PROG(snmpd2, snmpd, "not_found", /usr/sbin)
    snmpd=$snmpd2
fi

snmpd_valid="no"
if test "${snmpd}" = "not_found" ; then
    AC_MSG_WARN(snmpd not found:  snmp test scripts will not work)
elif test "${snmpd}" = "skip_check"; then
    AC_MSG_CHECKING(if snmpd can be started)
    AC_MSG_RESULT(skipped)
else
    AC_MSG_CHECKING(if snmpd can be started)
    # use the dlmod directive to see if the agent was compiled with dynamic
    # MIB support.  dummy MIB modules will only generate warnings
    echo 'dlmod dummy doubledummy' > conftestsnmp.conf
    test_ports='51510 51520 51530 51540 51550'
    snmpd_base_args="-f -r -l conftestsnmp.log -c conftestsnmp.conf"
    # On Linux snmpd seems to need the "-p" flag.
    for args in "" "-p"
    do
	for p in ${test_ports}
	do
		${snmpd} ${snmpd_base_args} ${args} udp:localhost:$p&
		pid=$!
		sleep 3
		if ps -p ${pid} > /dev/null
		then
			kill ${pid}
			snmpd_valid="yes"
			break 2
		fi
	done
    done
    if test "${snmpd_valid}" = "yes" ; then
	# Verify that dlmod directive was recognized
	if ( grep -i -e 'Unknown.*token.*dlmod' conftestsnmp.log ); then
	    echo "-------------------------------------------------------------"
	    echo "It looks like your Net-SNMP agent was not compiled with      "
	    echo "support for shared libraries.  This is option is enabled by  "
	    echo "default if your platform supports it.  You may try to recom- "
	    echo "compile Net-SNMP with the option --enable-shared.            "
            echo "If you are running Linux and Net-SNMP v 5.0.8 you may have   "
	    echo -n "to apply this patch: "
	    echo "http://sf.net/tracker/index.php?func=detail&aid=708409&group_id=12694&atid=112694"
	    echo ""
            echo "For Linux and Net-SNMP v 5.0.9 the patch is:"
	    echo "http://sourceforge.net/tracker/index.php?func=detail&aid=905573&group_id=12694&atid=456380"
	    echo ""
	    echo "If you don't know how to resolve the problem and do not want to "
	    echo "see this message again, run ./configure --without-snmp          "
	    echo "-------------------------------------------------------------"
	    snmpd_valid="no"
	fi
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
        echo "-------------------------------------------------------------   "
        echo "Unable to start snmpd. This is the snmpd log file:              "
	echo
	cat conftestsnmp.log
	echo ""
	echo "If you don't know how to resolve the problem and do not want to "
	echo "see this message again, run ./configure --without-snmp          "
        echo "-------------------------------------------------------------   "
    fi
fi

oldCPPFLAGS=${CPPFLAGS}
while test "${net_snmp_found}" = "true"; do
    NETSNMPCFLAGS=`net-snmp-config --cflags`
    # Darwin 7.0.0 / OS X 10.3 on ppc gives ppc and i386 arch flags which
    # the default compiler cannot compile.
    XR_CHECK_CFLAG(${NETSNMPCFLAGS}, snmpd_flags_valid=yes, snmpd_flags_valid=no)
    if test "${snmpd_flags_valid}" = "no" ; then
	echo "-------------------------------------------------------------   "
	echo "'net-snmp-config --cflags' gave invalid cflags!                 "
	echo "-------------------------------------------------------------   "
	snmpd_valid=no
	break
    fi

    CPPFLAGS=${NETSNMPCFLAGS}
    AC_CHECK_HEADERS([net-snmp/agent/net-snmp-agent-includes.h],
	net_snmp_agent_includes_bug=no,
        net_snmp_agent_includes_bug=yes,
        [
        #include <net-snmp/net-snmp-config.h>
        #include <net-snmp/net-snmp-includes.h>
        ]
    )
    if test "${net_snmp_agent_includes_bug}" = "yes" ; then
	AC_MSG_WARN(File <net-snmp/agent/net-snmp-agent-includes.h> does not appear usable)
	snmpd_valid=no
	break
    fi
    AC_CHECK_HEADERS([net-snmp/library/container.h],
	net_snmp_container_bug=no,
        net_snmp_container_bug=yes,
        [
        #include <net-snmp/net-snmp-config.h>
        #include <net-snmp/net-snmp-includes.h>
        #include <net-snmp/agent/net-snmp-agent-includes.h>
        ]
    )
    if test "${net_snmp_container_bug}" = "yes" ; then
	AC_MSG_WARN(File <net-snmp/library/container.h> does not appear usable)
	snmpd_valid=no
	break
    fi
    break
done
CPPFLAGS=${oldCPPFLAGS}

if test "${snmpd_valid}" = "yes" ; then
    MIBS="mibs"
else
    AC_MSG_WARN(XORP MIBs will not be built)
    MIBS=""
fi

AC_SUBST(MIBS)

dnl ---------------------------------------------------------------------------
dnl Check for libraries
dnl ---------------------------------------------------------------------------

dnl XXX Into C to avoid name mangling problems when checking libraries
AC_LANG_C

AC_SEARCH_LIBS(socket, socket)
AC_SEARCH_LIBS(inet_addr, nsl)
AC_SEARCH_LIBS(dlopen, dl)
AC_SEARCH_LIBS(hstrerror, resolv)

dnl Replace main with a function in -lc:
dnl AC_CHECK_LIB(c, main)
dnl XXX: XR_WITH_DMALLOC_DIR must be after -lc otherwise it won't have efect
dnl XXX: XR_WITH_DMALLOC_DIR is a local modified version of AM_WITH_DMALLOC
XR_WITH_DMALLOC_DIR
dnl Replace main with a function in -lutil:
dnl AC_CHECK_LIB(util, main)
dnl AC_CHECK_LIB(crypt, crypt)

dnl -- Back to C++ now we've finished checking for libraries
AC_LANG_CPLUSPLUS


dnl ---------------------------------------------------------------------------
dnl Check for system services
dnl ---------------------------------------------------------------------------

dnl-----------------------------
dnl Check for IPv6
dnl-----------------------------
ipv6=no
AC_MSG_CHECKING(whether the system has IPv6 stack)
if test "${enable_ipv6}" = "no"; then
  AC_MSG_RESULT(disabled)
else
  AC_LANG_SAVE
  AC_LANG_C
  AC_TRY_RUN([ /* AF_INET6 available check */
#include <stdlib.h>
#include <sys/types.h>
#include <sys/socket.h>
main()
{
 if (socket(AF_INET6, SOCK_STREAM, 0) < 0)
   return (1);
 else
   return (0);
}
],
  [AC_MSG_RESULT(yes)
   ipv6=yes],
  [AC_MSG_RESULT(no)],
  [AC_MSG_RESULT(no)])
  AC_LANG_RESTORE
fi

dnl
dnl Misc. IPv6-specific checks
dnl
if test "${ipv6}" = "yes"; then
  builtin(include, ../config/acipv6.m4)
fi


dnl ---------------------------------------------------------------------------
dnl Check for compiler characteristics
dnl ---------------------------------------------------------------------------

dnl ----------------------------
dnl GCC-specific warning flags
dnl ----------------------------

if test "${GCC}" = "yes" ; then
	dnl
	dnl Filter-out the "-g" flag
	dnl
	CFLAGS=`echo $CFLAGS |  sed 's/-g//g'`
	CXXFLAGS=`echo $CXXFLAGS |  sed 's/-g//g'`

	dnl
	dnl If debugging is enabled, try to add the "-g" flag
	dnl
	if test "${enable_debug}" = "yes" ; then
		XR_TRY_ADD_CFLAGS("-g")
		XR_TRY_ADD_CXXFLAGS("-g")
	fi

	dnl
	dnl TODO: add -ansi -pedantic
	dnl XXX: Flag -Wtraditional is not recommended for C, because:
	dnl	- On FreeBSD-4.5 macros like IN_CLASSA(i) from netinet/in.h
	dnl	  create a warning for gcc 2.95.3
	dnl	- On Linux RedHat 7.2, #elif creates a warning for gcc 2.96
	dnl
	dnl XXX: Flag -Wnon-const-format is not included, because it appears
	dnl	 to exist only on earlier versions of gcc (e.g., gcc-2.95.x),
	dnl	 and only on FreeBSD, but not on Linux. In addition, this flag
	dnl	 appears to be problematic, because if it is enabled we cannot
	dnl	 use statements like "vfprintf(stderr, fmt, ap);" where "fmt"
	dnl	 is a variable (e.g., defined as "const char *").
	dnl
	dnl XXX: For extra debugging, the following g++ flag may be enabled
	dnl	 and added to CXXFLAGS:
	dnl	 -D_GLIBCXX_DEBUG
	dnl
	CPARANOIDFLAGS="-W -Wall -Wwrite-strings -Wbad-function-cast -Wmissing-prototypes -Wcast-qual -Wmissing-declarations -Werror -Wpointer-arith -Wcast-align -Wstrict-prototypes -Wnested-externs"
	CXXPARANOIDFLAGS="-W -Wall -Wwrite-strings -Wcast-qual -Werror -Wpointer-arith -Wcast-align -Wstrict-prototypes -Woverloaded-virtual -Wtraditional"
	XR_TRY_ADD_CFLAGS($CPARANOIDFLAGS)
	XR_TRY_ADD_CXXFLAGS($CXXPARANOIDFLAGS)
	XR_TRY_ADD_CXXFLAGS("-ftemplate-depth-22")

	dnl
	dnl Try to add the -pipe option, which can significantly speed up
	dnl compile runs by avoiding the use of temporary files.
	dnl
	XR_TRY_ADD_CFLAGS("-pipe")
	XR_TRY_ADD_CXXFLAGS("-pipe")

	dnl
	dnl Default compiler flags have optimization turned on which
	dnl can cause helpful debugging info to be optimized out.
	dnl
	if test "${enable_optimize}" != "yes" ; then
		dnl Could just append -O0, but two -O args are icky
		dnl We make assumption change is O2 because extended regexps
		dnl are not portable...
		changequote(<<, >>)dnl preserve braces in sed expressions
		CFLAGS=`echo $CFLAGS | sed 's/-O2//g'`
		CXXFLAGS=`echo $CXXFLAGS | sed 's/-O2//g'`
		changequote([, ])dnl
	fi
	if test "${enable_profile}" = "yes" ; then
		CFLAGS="${CFLAGS} -pg"
		CXXFLAGS="${CXXFLAGS} -pg"
	fi
	if test "${enable_debug_fnames}" = "yes" ; then
		AC_DEFINE(DEBUG_PRINT_FUNCTION_NAME, 1,
			[Define to 1 to enable printing function name in debug messages])
	fi
fi


dnl ---------------------------------------------------------------------------
dnl Add files to under-go substitution here.
dnl ---------------------------------------------------------------------------

AC_OUTPUT(
	Makefile
	snmpdscripts/Makefile
	tests/Makefile
	tests/test_bgpmib.sh
	snmpdscripts/startsnmp
,
	echo timestamp > stamp-h
	chmod a+x tests/test_bgpmib.sh
	chmod a+x snmpdscripts/startsnmp
)

dnl ------------------------
dnl Print the compiler flags
dnl ------------------------
echo "Compiler C flags = $CFLAGS"
echo "Compiler C++ flags = $CXXFLAGS"

dnl ---------------------------------------------------------------------------
dnl END
dnl ---------------------------------------------------------------------------
