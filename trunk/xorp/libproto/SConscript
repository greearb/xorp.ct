# Copyright (c) 2009 XORP, Inc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, Version 2, June
# 1991 as published by the Free Software Foundation. Redistribution
# and/or modification of this program under the terms of any other
# version of the GNU General Public License is not permitted.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. For more details,
# see the GNU General Public License, Version 2, a copy of which can be
# found in the XORP LICENSE.gpl file.
#
# XORP Inc, 2953 Bunker Hill Lane, Suite 204, Santa Clara, CA 95054, USA;
# http://xorp.net

# $XORP$

import os
Import("env")

subdirs = [ 'tests' ]
SConscript(dirs = subdirs, exports='env')

env = env.Clone()
is_shared = env.has_key('SHAREDLIBS')

env.AppendUnique(CPPPATH = [ "#" ])
env.PrependUnique(LIBPATH = [ '$BUILDDIR/libxorp'] )
# XXX 'crypto' is a base system prerequisite; use LINKFLAGS?
env.AppendUnique(LIBS = ['xorp_core', 'crypto'])

sources = [
	'checksum.c',
	'packet.cc',
	'proto_node_cli.cc',
	'proto_state.cc',
	'proto_unit.cc',
	'spt.cc',
	]

if is_shared:
    libxorp_proto = env.SharedLibrary(target = 'libxorp_proto',
				      source = sources)
    if env['rtld_origin']:
        for obj in libxorp_proto:
            env.AddPostAction(libxorp_proto,
                env.Symlink(obj.abspath, env['BUILDDIR'] + '/lib/' + str(obj)))
else:
    libxorp_proto = env.StaticLibrary(target = 'libxorp_proto',
				      source = sources)

if is_shared:
    env.Alias("install",
        env.InstallLibrary('$libdir', libxorp_proto))

Default(libxorp_proto)
